apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: 'signing.gradle'

android {
    compileSdkVersion 28

    def versionPropsFile = file('version.properties')
    def versionBuild

    // set versionBuild (read from file)
    if (versionPropsFile.canRead())
    {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    }
    else
    {
        throw new FileNotFoundException("Could not read version.properties")
    }

    // increment version number, only runs on build
    ext.autoIncrementBuildNumber = {
        if (versionPropsFile.canRead())
        {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        }
        else
        {
            throw new FileNotFoundException("Could not read version.properties")
        }
    }

    defaultConfig {
        applicationId "sancho.gnarlymusicplayer"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 5
        versionName "1.2.1." + versionBuild // major.minor.patch.build
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    gradle.taskGraph.whenReady {taskGraph ->
        if (taskGraph.hasTask(assembleDebug))
        {
            // debug task
            autoIncrementBuildNumber()
        }
        else if (taskGraph.hasTask(assembleRelease))
        {
            // release task
            autoIncrementBuildNumber()
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.61'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.media2:media2:1.0.0-alpha04'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'androidx.preference:preference:1.1.0'
}
